plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'groovy'
}

group = 'io.github.kamilszewc'
version = '0.5'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    // JUnit
    testImplementation platform('org.junit:junit-bom:5.11.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // Testing
    testImplementation 'org.apache.groovy:groovy-all:4.0.24'
    testImplementation 'org.spockframework:spock-core:2.4-M4-groovy-4.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                if (project.properties.containsKey('mavenCentralTokenUser')) {
                    username = "$mavenCentralTokenUser"
                }
                if (project.properties.containsKey('mavenCentralTokenPassword')) {
                    password = "$mavenCentralTokenPassword"
                }
            }
        }
    }
    publications {

        mavenJava(MavenPublication) {
            artifactId = 'jarutils'
            from components.java
            pom {
                name = 'Jar Utils'
                description = 'Jar Utils - a set of random helper functions to manage jar files'
                url = 'https://github.com/computinglaboratory/jarutils'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kszewc'
                        name = "Kamil Szewc"
                        email = 'kamil.szewc@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/kamilszewc/jarutils.git'
                    developerConnection = 'scm:git:https://github.com/kamilszewc/jarutils.git'
                    url = 'https://github.com/kamilszewc/jarutils.git'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
    sign configurations.archives
}